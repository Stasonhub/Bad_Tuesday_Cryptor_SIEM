
event Psexesvc_started:
    filter {
        correlation_name == null and
        event_src.host != null and
        msgid == "4688" and
        lower(object.name) == "psexesvc.exe" and
        event_src.title == "windows"
    }
    
event Schtasks_started:
    filter {
        correlation_name == null and
        event_src.host != null and
        msgid == "4688" and
        lower(object.name) == "schtasks.exe" and
        event_src.title == "windows"
    }
    
rule Potential_attack_psexesvc_schtasks: ((Psexesvc_started[10] with different event_src.host) or (Schtasks_started[10] with different event_src.host)) within 5m
    
    on Psexesvc_started {
        $subject.name = subject.name
        $subject.domain = subject.domain
        
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
    }
    
    on Schtasks_started {
        $subject.name = subject.name
        $subject.domain = subject.domain
                
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
    }
    
emit {
    $correlation_name = "Potential_attack_psexesvc_schtasks"
    $correlation_type = "incident"
    
    $subject = "account"
    $action = "initiate"
    $object = "attack"
    $status = "success"
    
    $category.generic = "Malware"
    $category.high = "Miscellaneous"
    $category.low = "Detection"
    
    $id = "PT_SIEM_Potential_attack_psexesvc_schtasks"
    $importance = "high"
}
